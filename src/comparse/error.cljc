(ns comparse.error
  (:require [clojure.string :as str]))

(defprotocol IMessage
  (-render [this]))

(deftype Expected [label]
  IMessage
  (-render [_] (str "Expected: " label)))

(deftype ExpectedString [s]
  IMessage
  (-render [_] (str "Expected: '" s "'")))

(deftype Unexpected [label]
  IMessage
  (-render [_] (str "Unexpected: " label)))

(deftype UnexpectedString [s]
  IMessage
  (-render [_] (str "Unexpected: '" s "'")))

(deftype Message [msg]
  IMessage
  (-render [_] msg))

(deftype Nested [pos user-state msgs]
  IMessage
  (-render [_] "<nested>"))

(deftype Compound [label pos user-state msgs]
  IMessage
  (-render [_] "<compound>"))

(defn expected
  "The input does not match the expected input.

   `label` describes the expected input."
  [label]
  (list (Expected. label)))

;; fparsec: + ExpectedCaseInsensitiveString
(defn expected-str
  "The input does not match an expected string constant."
  [str]
  (list (ExpectedString. str)))

(defn unexpected
  "The parser encountered some unexpected input."
  [label]
  (list (Unexpected. label)))

;; fparsec: + UnexpectedCaseInsensitiveString
(defn unexpected-str
  "The parser encountered some unexpected input."
  [str]
  (list (UnexpectedString. str)))

(defn message
  "The error does not fit the other types."
  [msg]
  (list (Message. msg)))

(defn nested
  "Backtracked after an error occurred."
  [pos user-state msgs]
  (list (Nested. pos user-state msgs)))

(defn compound
  "Mainly generated by compound-labelling operator."
  [label pos user-state msgs]
  (list (Compound. label pos user-state msgs)))

;; fparsec: + Other

(def ^:private eof-label "end of input")
(def unexpected-eof (unexpected eof-label))
(def expected-eof (expected eof-label))

;; fparsec: ToHashSet, ToSortedArray
;; Duplicate error messages and empty Expected...,
;; Unexpected... and Message messages are filtered out

(defn merge-messages [msgs1 msgs2]
  ;; TODO: Optimise
  (concat msgs1 msgs2))

(defn render-messages [msgs]
  ;; TODO: Sort/dedupe
  ;; TODO: combine with "or" / "one of"
  (str/join ", " (map -render msgs)))

;; TODO: Pretty-printing with source line and ^ marker
